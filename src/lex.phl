module lex;

import phl::lang::collections;
import files;
import err;

@String fromChar(@Integer char) [
	@Pointer<@Integer> ptr = @Pointer<@Integer> alloc(ptr_size());
	ptr:=char;
	return @String ptr;
]

/* erota sanat toisistaan */
@String[] lex(@String code) [
	var a = new @String[];
	
	var currWord = "";
	
	var len = code::len;
	for (var i = 0; i < len; i=i+1) {
		var ch = code[i];
		
		if (ch == ' ' || ch == '\t') {
			if (currWord::len > 0) a.add(currWord);
			currWord = "";
		} else if (ch == '#' || ch == ':' || ch == ',' || ch == '(' || ch == ')') {
			if (currWord::len > 0) a.add(currWord);
			currWord = "";
			a.add(fromChar(ch));
		} else if (ch == '\n') {
			if (currWord::len > 0) a.add(currWord);
			currWord = "";
			a.add("(NL)");
		} else if (ch == '"') { /* '"' */
			if (currWord::len > 0) a.add(currWord); currWord = "";
			
			var str = "\"";
			
			i = i + 1;
			while (code[i] != '"') /* '"' */ {
				str = str.appendch(code[i]);
				i = i + 1;
			}
			a.add(str + "\"");
		} else if (ch == '\\') {
			if (currWord::len > 0) a.add(currWord);
			currWord = "";
			i = i + 1;
			/* alla oleva ei palvele tarkoitusta, ehkä jotain\ jotain olisi yksi tokeni? */
			doc a.add(fromChar(code[i]));
		} else if (ch == ';') {
			if (currWord::len > 0) a.add(currWord);
			currWord = "";
			do { i=i+1; ch = code[i]; } while (ch != '\n');
			a.add("(NL)");
		} else {
			currWord = currWord.appendch(ch);
		}
	}
	
	if (currWord::len > 0) a.add(currWord);
	currWord = "";
	
	return a;
]

/* Yksinkertainen esikääntäjä */

class @PPMacro {
	field @String[] params;
	field @String[] body;
	
	@PPMacro init(@String[] params, @String[] body) [
		this::params = params;
		this::body = body;
		return this;
	]
	
	@String[] eval(@String[][] args) [
		if (null != this::params && null != args) {
			if (args::size != this::params::size) err("?: wrong number of macro parameters");
			
			var tmp = macros;
			macros = new @PPScope.init(null);
		
			args iterate #(i, arg) [
				macros::macros[this::params[i]] = new @PPMacro.init(null, arg);
			];
		
			var a = preprocess_macro(new @String[].addAll(this::body));
			macros = tmp;
			return a;
		}
		
		return this::body;
	]
	
	auto @String [
		if (null != params())
			return "" + params() + "" + body();
		else return "[]" + body();
	]
};

class @CounterMacro : @PPMacro {

	field @Integer counter;
	
	new [
		this::counter = 0;
	]
	
	@String[] eval(@String[][] args) [
		this::counter = this::counter + 1;
		return new @String[].."("+this::counter+")";
	]
	
	auto @String [
		return "[COUNTER:"+this::counter+"]";
	]
};

class @LenMacro : @PPMacro {
	
	new [
		this::params = new @String[].."p#str";
	]
	
	@String[] eval(@String[][] args) [
		if (null == args) err("len: wrong number of macro parameters");
		return new @String[]..""+args[0]::size;
	]
	
	auto @String [
		return "[LEN]";
	]
};

struct @PPScope {
	field @PPScope super_;
	field @Hashtable<@PPMacro> macros;
	
	new [ this::macros = new @Hashtable<@PPMacro>; ]
	
	@PPScope init(@PPScope super_) [
		this::super_ = super_;
		return this;
	]
	
	@PPMacro getMacro(@String name) [
		var macro = this::macros[name];
		if (null != macro) return macro;
		else if (null != super_()) return super_().getMacro(name);
		else return null;
	]
};

static @PPScope macros;

@String[] preprocess(@String[] a) [
	if (null == macros) {
		macros = new @PPScope.init(null);
		macros::macros["__COUNTER"] = (new @CounterMacro);
	}
	
	macros::macros["__LEN"] = null;
	
	var asize = a::size;
	
	var print = true; var ifdepth = 0;
	var istack = new @Integer[];
	
	for (var i = 0; i < asize; i=i+1) {
		if (print::n) {
			if (i+1 >= asize) err("directive syntax error");
			if (a[i] == "#" && a[i+1] == "ifdef" || a[i+1] == "ifndef") {
				ifdepth = ifdepth + 1;
			}
			if (a[i] == "#" && a[i+1] == "endif") {
				if (ifdepth == 1) {
					print = true;
					a.remove(i);
				}
				else ifdepth = ifdepth - 1;
			}
			a.remove(i);
			
			i = i - 1;
			asize = a::size;
		}
		else if (a[i] == "#") {
			a.remove(i);
			if (i == asize-1) err("directive syntax error");
			var directive = a.remove(i);
			if (directive == "define") {
				if (i == asize-2) err("directive:define syntax error");
				var macro_name = a.remove(i);
				var macro_params = @String[] null;
				if (i == asize-3) err("directive:define syntax error");
				if (a[i] == "(") {
					macro_params = new @String[];
					if (i == a::size) err("directive:define syntax error");
					while (a.remove(i) != ")") {
						if (i == a::size) err("directive:define syntax error");
						macro_params.add("p#"+a.remove(i));
					}
				}
				var macro_body = new @String[];
				if (i == a::size) err("directive:define syntax error");
				while (a[i] != "(NL)") {
					if (i == a::size) err("directive:define syntax error");
					if (macro_params!=null)
						if (macro_params.contains("p#"+a[i])) macro_body.add("p#" + a.remove(i));
						else macro_body.add(a.remove(i));
					else macro_body.add(a.remove(i));
				}
				if (i == a::size) err("directive:define syntax error");
				a.remove(i);
				macros::macros[macro_name] = new @PPMacro.init(macro_params, macro_body);
			}
			else if (directive == "undefine") {
				if (i == asize-2) err("directive:undefine syntax error");
				var macro_name = a.remove(i);
				macros::macros[macro_name] = null;
			}
			else if (directive == "ifdef") {
				if (i == asize-2) err("directive:ifdef syntax error");
				var macro_name = a.remove(i);
				if (macros::macros[macro_name] == null) print = false;
				ifdepth = 1;
			}
			else if (directive == "ifndef") {
				if (i == asize-2) err("directive:ifndef syntax error");
				var macro_name = a.remove(i);
				if (macros::macros[macro_name] != null) print = false;
				ifdepth = 1;
			}
			else if (directive == "endif") {
				print = false;
				ifdepth = 0;
			}
			else if (directive == "include") {
				var file = a.remove(i);
				if (file[0] == '"') {
					file = file::copy.substring(1);
					(@Pointer<@Integer> file)[file::len-1] = 0;
				}
				a.insertAll(i, lex(read_file(file)));
			}
			else if (directive == "<") {
				istack.add(i);
				i = i + 2;
			}
			else if (directive == ">") {
				i = istack.remove(-(1));
			}
			else err("unknown directive '" + directive + "'");
			i = i - 1;
			asize = a::size;
		} else if (a[i][0] == '"') {
			var str = a.remove(i);
			var j = 1;
			var len = str::len;
			while (j < len-1) {
				a.insert(i+j-1, @String str[j]);
				j = j + 1;
			}
			
			i = i - 1;
			asize = a::size;
		} else if (a[i] == "(NL)") {
			a.remove(i);
			i = i - 1;
			asize = a::size;
		} else {
			var macro = macros.getMacro(a[i]);
			if (null != macro) {
				doc printf("found macro: %s %s\n", a[i], @String macro);
				a.remove(i);
				if (i < asize-1) {
					if (a[i] == "(" && null != macro::params) {
						a.remove(i);
						var macro_args = new @String[][];
					
						if (i == asize-2) err("macro syntax error");
						while (true) {
							var arg = new @String[];
							macro_args.add(arg);
							var depth = 0;
							if (i == a::size) err("macro syntax error");
							while (((a[i] == "," || a[i] == ")") && depth <= 0)::n) {
								var c = a[i];
								if (c == "(") depth = depth + 1;
								else if (c == ")") depth = depth - 1;
								a.remove(i);
								if (i == a::size) err("macro syntax error");
								arg.add(c);
							}
							
							if (a.remove(i) == ")") break;
						}
						a.insertAll(i, macro.eval(macro_args));
					} else {
						a.insertAll(i, macro.eval(null));
					}
				}
				else {
					a.insertAll(i, macro.eval(null));
				}
				i = i - 1;
				asize = a::size;
			}
		}
	}
	
	macros::macros["__LEN"] = (new @LenMacro);
	
	var macro_existed = false;
	do {
		macro_existed = false;
		for (var i = 0; i < asize; i=i+1) {
			if (a[i] == ":") {
				a.remove(i);
				var macro_name = a.remove(i);
				var macro_body = new @String[];
				macro_body.add(""+i);
				macros::macros[macro_name] = new @PPMacro.init(null, macro_body);
				i = i - 1;
				asize = a::size;
			} else {
				var macro = macros.getMacro(a[i]);
				if (null != macro) {
					macro_existed = true;
					a.remove(i);
					if (i < asize-1) {
						if (a[i] == "(" && null != macro::params) {
							a.remove(i);
							var macro_args = new @String[][];
					
							if (i == asize-2) err("macro syntax error");
							while (true) {
								var arg = new @String[];
								macro_args.add(arg);
								var depth = 0;
								if (i == a::size) err("macro syntax error");
								while (((a[i] == "," || a[i] == ")") && depth <= 0)::n) {
									var c = a[i];
									if (c == "(") depth = depth + 1;
									else if (c == ")") depth = depth - 1;
									a.remove(i);
									if (i == a::size) err("macro syntax error");
									arg.add(c);
								}
					
								if (a.remove(i) == ")") break;
							}
							a.insertAll(i, macro.eval(macro_args));
						} else {
							a.insertAll(i, macro.eval(null));
						}
					}
					else {
						a.insertAll(i, macro.eval(null));
					}
					i = i - 1;
					asize = a::size;
				}
			}
		}
	} while (macro_existed);
	return a;
]

@String[] preprocess_macro(@String[] a) [
	var asize = a::size;
	
	for (var i = 0; i < asize; i=i+1) {
		var macro = macros.getMacro(a[i]);
		if (null != macro) {
			a.remove(i);
			if (i < asize-1) {
				if (a[i] == "(" && null != macro::params) {
					a.remove(i);
					var macro_args = new @String[][];
					
					if (i == asize-2) err("macro syntax error");
					while (true) {
						var arg = new @String[];
						macro_args.add(arg);
						var depth = 0;
						if (i == a::size) err("macro syntax error");
						while (((a[i] == "," || a[i] == ")") && depth <= 0)::n) {
							var c = a[i];
							if (c == "(") depth = depth + 1;
							else if (c == ")") depth = depth - 1;
							a.remove(i);
							if (i == a::size) err("macro syntax error");
							arg.add(c);
						}
					
						if (a.remove(i) == ")") break;
					}
					a.insertAll(i, macro.eval(macro_args));
				} else {
					a.insertAll(i, macro.eval(null));
				}
			}
			else {
				a.insertAll(i, macro.eval(null));
			}
			i = i - 1;
			asize = a::size;
		}
	}
	return a;
]
